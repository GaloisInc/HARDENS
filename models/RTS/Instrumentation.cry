module RTS::Instrumentation where

import RTS::Utils

/* Behavioral model of the RTS instrumentation unit.

   The transitions of the state machine are the Set/Toggle events, plus sampling the sensors.
*/

type Input = [2][32]

type NChannels = 3
type Channel = [lg2 NChannels]
T,P,S : Channel
T = 0 // Temperature
P = 1 // Pressure
S = 2 // Saturation

type NModes = 3
type Mode = [lg2 NModes]
Bypass,Operate,Manual: Mode
Bypass = 0 // Do not generate a "trip"
Operate = 1 // generate "trip" on setpoint violation
Manual = 2 // Force "trip" generation

type Instrumentation =
  { setpoints: [NChannels][32]
  , reading: [NChannels][32]
  , mode: [NChannels]Mode
  , sensor_trip: [NChannels]
  , output_trip: [NChannels][8]
  , maintenance: Bit
  }

type CommandType = [lg2 NCommands]
type NCommands = 4
Set_Mode_Cmd, Set_Maintenance_Cmd, Set_Setpoint_Cmd, Null_Cmd : CommandType
Set_Mode_Cmd = 0
Set_Maintenance_Cmd = 1
Set_Setpoint_Cmd = 2
Null_Cmd = 3

type Command =
  { command: CommandType
  , channel: Channel
  , mode: Mode
  , setpoint: [32]
  , on_off: Bit
  }

NoCommand: Command
NoCommand = { command= Null_Cmd, channel= zero, mode= zero, setpoint= zero, on_off= zero }

Initial: Instrumentation
Initial =
  { setpoints   = zero
  , reading     = zero
  , mode        = zero
  , sensor_trip = zero
  , output_trip = repeat zero
  , maintenance = ~zero
  }

Step: Input -> Command -> Instrumentation -> Instrumentation
Step inp cmd instr =
       Read_Input inp instr
  >>>> Step_Trip_Signals
  >>>> Handle_Command cmd
  >>>> OutputTrips
  where
    OutputTrips state =
      { state | output_trip = [ zero # [Is_Ch_Tripped (state.mode @ ch) (state.sensor_trip @ ch)] | ch <- [0..2] ] }

Read_Input : Input -> Instrumentation -> Instrumentation
Read_Input sensors instr = { instr | reading = vals }
  where
    vals: [3][32]
    // 0 and 1 should be T and P, but this causes problems for crymp
    vals = [ sensors @ 0
           , sensors @ 1
           , Saturation (sensors @ 0) (sensors @ 1)
           ]

Step_Trip_Signals:
  Instrumentation ->
  Instrumentation
Step_Trip_Signals state =
  { state | sensor_trip = sensor_trips }
  where
    sensor_trips = Generate_Sensor_Trips state.reading state.setpoints

Handle_Command: Command -> Instrumentation -> Instrumentation
Handle_Command cmd instr =
  if (cmd.command == Set_Mode_Cmd) && (cmd.channel < `NChannels) then
    Set_Mode cmd.channel cmd.mode instr
  else if cmd.command == Set_Maintenance_Cmd then
    Set_Maintenance cmd.on_off instr
  else if (cmd.command == Set_Setpoint_Cmd) && (cmd.channel < `NChannels) then
    Set_Setpoint cmd.channel cmd.setpoint instr
  else
    instr

////////////////////////////////////////
// Queries, "Setters", "Getters", etc
////////////////////////////////////////

Get_Reading: Instrumentation -> [NChannels][32]
Get_Reading instr = instr.reading

In_Maintenance: Instrumentation -> Bit
In_Maintenance instr = instr.maintenance

Set_Maintenance: Bit -> Instrumentation -> Instrumentation
Set_Maintenance on instr = { instr | maintenance = on }

Set_Mode: Channel -> Mode -> Instrumentation -> Instrumentation
Set_Mode ch mode i =
  if In_Maintenance i && (mode <= 2) then
    {i | mode = update i.mode ch mode}
  else
    i

In_Mode: Channel -> Mode -> Instrumentation -> Bit
In_Mode ch mode instr = (instr.mode @ ch) == mode

Get_Setpoint: Channel -> Instrumentation -> [32]
Get_Setpoint ch instr = instr.setpoints @ ch

Set_Setpoint: Channel -> [32] -> Instrumentation -> Instrumentation
Set_Setpoint ch val instr = { instr | setpoints = update instr.setpoints ch val }

Get_Tripped: Instrumentation -> [NChannels][8]
Get_Tripped instr = [zero # [Is_Tripped T instr], zero # [Is_Tripped P instr], zero # [Is_Tripped S instr]]

Is_Tripped: Channel -> Instrumentation -> Bit
Is_Tripped ch instr = In_Mode ch Manual instr
                   || (In_Mode ch Operate instr && (instr.sensor_trip @ ch))

Is_Ch_Tripped : Mode -> Bit -> Bit
Is_Ch_Tripped mode sensor_tripped =
  // Constants should be replaced, but this causes problems for crymp
  // 2 = Manual, 1 = Operate
  (mode == 2) || ((mode == 1) && sensor_tripped)

Saturation : [32] -> [32] -> [32]
Saturation t p = if p == 0 then 0 else (t / p)

Generate_Sensor_Trips : [NChannels][32] -> [NChannels][32] -> [NChannels]
Generate_Sensor_Trips vals setpoints =
  [ trp ch | ch <- [0..NChannels-1] : [3][2] ]
  where
    // 2 should be S, but this causes problems for crymp
    trp ch = if ch == 2 then v < sp else sp < v
               where v  = vals @ ch
                     sp = setpoints @ ch

private
  /** @requirements
        INSTRUMENTATION_RESET
  */
  property instrumentation_reset =
    In_Maintenance Initial
    /\ In_Mode P Bypass Initial
    /\ In_Mode T Bypass Initial
    /\ In_Mode S Bypass Initial

  /** @requirements
        INSTRUMENTATION_TRIP_PRESSURE
  */
  property instrumentation_trip_pressure (inp: Input) (instr: Instrumentation) =
       In_Mode P Manual instr
    \/ (In_Mode P Operate instr /\ inp @ P > Get_Setpoint P instr')
    /* ------------------------------------------------------------------- */ ==
    (Is_Tripped P instr')
      where instr' = Read_Input inp instr >>>> Step_Trip_Signals

  /** @requirements
        INSTRUMENTATION_TRIP_TEMPERATURE
  */
  property instrumentation_trip_temperature (inp: Input) (instr: Instrumentation) =
          In_Mode T Manual instr
     \/  (In_Mode T Operate instr /\ inp @ T > Get_Setpoint T instr')
    /* ------------------------------------------------------------------- */ ==
    (Is_Tripped T instr')
      where instr' = Read_Input inp instr >>>> Step_Trip_Signals

  /** @requirements
        INSTRUMENTATION_TRIP_SATURATION
  */
  property instrumentation_trip_saturation (inp: Input) (instr: Instrumentation) =
       In_Mode S Manual instr
       \/ (In_Mode S Operate instr /\ Saturation (inp @ T) (inp @ P) < Get_Setpoint S instr')
    /* ------------------------------------------------------------------- */ ==
    (Is_Tripped S instr')
      where instr' = Read_Input inp instr >>>> Step_Trip_Signals

  /** @requirements
        INSTRUMENTATION_SET_MANUAL_TRIP_TEMPERATURE
        INSTRUMENTATION_SET_MANUAL_TRIP_PRESSURE
        INSTRUMENTATION_SET_MANUAL_TRIP_SATURATION
  */
  property instrumentation_set_manual_trip (instr: Instrumentation) =
    In_Maintenance instr ==> (
         (Is_Tripped T trippedT \/ ~ (instr.sensor_trip @ T))
      /\ (Is_Tripped P trippedP \/ ~ (instr.sensor_trip @ P))
      /\ (Is_Tripped S trippedS \/ ~ (instr.sensor_trip @ S))
    )
    where
      trippedT = Set_Mode T Manual instr
      trippedP = Set_Mode P Manual instr
      trippedS = Set_Mode S Manual instr

  /** @requirements
         INSTRUMENTATION_SET_SETPOINT_TEMPERATURE
         INSTRUMENTATION_SET_SETPOINT_PRESSURE
         INSTRUMENTATION_SET_SETPOINT_SATURATION
  */
  property get_set_setpoint_correct (instr: Instrumentation) (val: [32]) =
       Get_Setpoint T (Set_Setpoint T val instr) == val
    /\ Get_Setpoint P (Set_Setpoint P val instr) == val
    /\ Get_Setpoint S (Set_Setpoint S val instr) == val

  /** @requirements
         INSTRUMENTATION_SET_BYPASS_TEMPERATURE
         INSTRUMENTATION_SET_BYPASS_PRESSURE
         INSTRUMENTATION_SET_BYPASS_SATURATION
  */
  property set_bypass_correct (instr: Instrumentation) =
       In_Maintenance instr ==> (
            ~ Is_Tripped T (Set_Mode T Bypass instr)
         /\ ~ Is_Tripped P (Set_Mode P Bypass instr)
         /\ ~ Is_Tripped S (Set_Mode S Bypass instr)
       )

  property step_state_const (inp: Input) (instr: Instrumentation) =
         instr.mode == instr'.mode
      /\ instr.setpoints == instr'.setpoints
      /\ instr.maintenance == instr'.maintenance
      where instr' = Read_Input inp instr >>>> Step_Trip_Signals

  // Not connected to a high level requirement
  property is_ch_trip_correct instr =
       Is_Tripped T instr == Is_Ch_Tripped (instr.mode @ T) (instr.sensor_trip @ T)
    /\ Is_Tripped P instr == Is_Ch_Tripped (instr.mode @ P) (instr.sensor_trip @ P)
    /\ Is_Tripped S instr == Is_Ch_Tripped (instr.mode @ S) (instr.sensor_trip @ S)
